[site]: stackoverflow
[post_id]: 5724744
[parent_id]: 5724373
[tags]: 
First, I don't think you're using PERMUT correctly here. What PERMUT(6,3) gives you is the total number of ways to arrange three things picked out of a set of six things. So the result is 120 because you could have 6*5*4 possible permutations. In your case you have 3^6 = 729 possible strings, because each position has one of three possible characters. Others have posted perfectly fine VBA-based solutions, but this isn't that hard to do in the worksheet. Here is an array formula that will return an array of the last six digits of the ternary (base-3) representation of a number: =FLOOR(MOD( ,3^({5,4,3,2,1,0}+1))/(3^{5,4,3,2,1,0}),1) (As WarrenG points out, just getting a bunch of base-3 numbers is one way to solve your problem.) You would drag out the numbers 0 through 728 in a column somewhere, say $A$1:$A$729. Then in $B$1:$G$1, put the formula: =FLOOR(MOD(A1,3^({5,4,3,2,1,0}+1))/(3^{5,4,3,2,1,0}),1) remembering to enter it as an array formula with Ctrl-Shift-Enter. Then drag that down through $B$729:$G$729. Finally in cell $H$1, put the formula: =CONCATENATE(B1,C1,D1,E1,F1,G1) and drag that down through $H$729. You're done!
